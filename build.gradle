buildscript {
    ext {
        springBootVersion = '1.5.8.RELEASE'
        alpnAgentVersion  = '2.0.6'
        jettyVersion      = '9.4.7.v20170914'
    }

    repositories {
        jcenter()
        maven { url "https://repo.spring.io/release" }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

plugins {
  id 'java'
  id 'application'
  id 'eclipse'
  id 'idea'
}
apply plugin: 'org.springframework.boot'


group = 'org.test'
version = '0.0.1-SNAPSHOT'

description = "demo-http2"

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    javaAgent { description "additional lib as jvm agent" }
}

dependencyManagement {
    imports {
        ext['jetty.version'] = "${jettyVersion}"
    }
}

repositories {
     jcenter()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter')
    compile('org.springframework:spring-webmvc')

    // jetty
    compile('org.springframework.boot:spring-boot-starter-jetty')
    compile("org.eclipse.jetty.http2:http2-server:${jettyVersion}")
    compile("org.eclipse.jetty:jetty-alpn-server:${jettyVersion}")

    testCompile('org.springframework.boot:spring-boot-starter-test') {
        exclude(module: 'commons-logging')
    }

    testCompile 'com.squareup.okhttp3:okhttp:3.4.1'

    // ALPN
    compile("org.mortbay.jetty.alpn:jetty-alpn-agent:${alpnAgentVersion}")
    javaAgent("org.mortbay.jetty.alpn:jetty-alpn-agent:${alpnAgentVersion}")
}

defaultTasks 'bootRun'
mainClassName = 'demo.DemoHttp2Application'
applicationDefaultJvmArgs = ["-javaagent:lib/jetty-alpn-agent-${alpnAgentVersion}.jar"]

test {
    systemProperty "javax.net.ssl.trustStoreType", "jks"
    systemProperty "javax.net.ssl.trustStore", "src/main/resources/sample.jks"
    systemProperty "javax.net.debug", "ssl"
    systemProperty "javax.net.ssl.trustStorePassword", "changeit"
    jvmArgs = ["-javaagent:lib/jetty-alpn-agent-${alpnAgentVersion}.jar"]
}

distributions {
    main.contents {
        from(configurations.javaAgent) {
            into "lib"
        }
    }
}

task copyAlpnAgentJar (type:Copy) {
    from (configurations.javaAgent)
    into("lib")
}

tasks["eclipse"].dependsOn(copyAlpnAgentJar)
processResources.dependsOn copyAlpnAgentJar

clean {
    delete "lib/*.jar"
}
